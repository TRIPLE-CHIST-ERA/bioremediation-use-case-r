PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX vocab: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX endpoint: <https://idsm.elixir-czech.cz/sparql/endpoint/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>

SELECT DISTINCT
    ?similar_compound_chebi
WHERE
{
    {
        # Pollutant CAS numbers retrieved from wikidata.
        SERVICE <https://query.wikidata.org/sparql> { 
            SELECT DISTINCT
                ?cas_number
            WHERE
            {
                ?compound  wdt:P31                      wd:Q113145171 ;
                           wdt:P232                     ?ec_number    ;
                           wdt:P231                     ?cas_number   ;
                           wdt:P2240                    ?ld50         ;
                           wdt:P366                     ?use_type     ;
                           p:P2240                      ?ref          .
                ?use_type  wdt:P279*                    wd:Q131656    .
                ?ref       pq:P636                      wd:Q285166    ;
                           (pq:P689|pq:P2352)/wdt:P279  wd:Q184224    .
            }
        }

        # * sio:SIO_000011     ->  'is attribute of'
        # * sio:SIO_000300     ->  'has value'
        # * sio:CHEMINF_000446 ->  'CAS registry number'
        ?synonym  sio:SIO_000300  ?cas_number               ;
                  a               sio:CHEMINF_000446 ;
                  sio:SIO_000011  ?compound          .
        ?compound a               vocab:Compound     .

        # SIO_011120  ->  'molecular structure file'
        ?attribute  a               sio:SIO_011120 ;  
                    sio:SIO_000011  ?compound      ;
                    sio:SIO_000300  ?molfile       .

        SERVICE endpoint:chebi {
            [
                sachem:compound ?similar_compound_chebi ;
                sachem:score    ?score
            ] sachem:similaritySearch [
                sachem:query             ?molfile                          ;
                sachem:cutoff            '0.7'^^xsd:double                 ;
                sachem:similarityRadius  '3'^^xsd:integer                  ;
                sachem:aromaticityMode   sachem:aromaticityDetectIfMissing ;
                sachem:tautomerMode      sachem:inchiTautomers
            ] .
        }
    }

    UNION
    {
        SERVICE endpoint:chebi {
          ?similar_compound_chebi sachem:substructureSearch [ sachem:query '[As]' ] .
        }
    }
}
