PREFIX CHEBI: <http://purl.obolibrary.org/obo/CHEBI_>
PREFIX endpoint: <https://idsm.elixir-czech.cz/sparql/endpoint/>
PREFIX p: <http://www.wikidata.org/prop/>
PREFIX pq: <http://www.wikidata.org/prop/qualifier/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rh: <http://rdf.rhea-db.org/>
PREFIX sachem: <http://bioinfo.uochb.cas.cz/rdf/v1.0/sachem#>
PREFIX sio: <http://semanticscience.org/resource/>
PREFIX up: <http://purl.uniprot.org/core/>
PREFIX vocab: <http://rdf.ncbi.nlm.nih.gov/pubchem/vocabulary#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

SELECT DISTINCT
    ?uniprot
WHERE
{
    SERVICE <https://sparql.rhea-db.org/sparql> {
    SELECT DISTINCT
        ?rhea
    WHERE
    {

        SERVICE <https://idsm.elixir-czech.cz/sparql/endpoint/idsm> {
        SELECT DISTINCT
            ?similar_compound_chebi
        WHERE
        {
            {
                # Retrieve pollutant CAS numbers from wikidata.
                SERVICE <https://query.wikidata.org/sparql> { 
                SELECT DISTINCT
                    ?cas_number
                WHERE
                {
                    ?compound wdt:P31                     wd:Q113145171 ;
                                wdt:P232                    ?ec_number    ;
                                wdt:P231                    ?cas_number   ;
                                wdt:P2240                   ?ld50         ;
                                wdt:P366                    ?use_type     ;
                                p:P2240                     ?ref          .
                    ?use_type wdt:P279*                   wd:Q131656    .
                    ?ref      pq:P636                     wd:Q285166    ;
                                (pq:P689|pq:P2352)/wdt:P279 wd:Q184224    .
                }
                }

                ?synonym   sio:SIO_000300 ?cas_number        ;
                            a              sio:CHEMINF_000446 ;
                            sio:SIO_000011 ?compound          .
                ?compound  a              vocab:Compound     .
                ?attribute a              sio:SIO_011120     ;  
                            sio:SIO_000011 ?compound          ;
                            sio:SIO_000300 ?molfile           .

                SERVICE endpoint:chebi {
                    [
                        sachem:compound ?similar_compound_chebi ;
                        sachem:score    ?score
                    ] sachem:similaritySearch [
                        sachem:query             ?molfile                          ;
                        sachem:cutoff            '0.7'^^xsd:double                 ;
                        sachem:similarityRadius  '3'^^xsd:integer                  ;
                        sachem:aromaticityMode   sachem:aromaticityDetectIfMissing ;
                        sachem:tautomerMode      sachem:inchiTautomers
                    ] .
                }
            }

            UNION
            {
                SERVICE endpoint:chebi {
                ?similar_compound_chebi sachem:substructureSearch [ sachem:query '[As]' ] .
                }
            }
        }
        }
        # The ChEBI can be used either as a small molecule, the reactive
        # part of a macromolecule, or as a polymer.
        ?compound ( rh:chebi | 
                    (rh:reactivePart/rh:chebi) |
                    (rh:underlyingChebi/rh:chebi) ) ?similar_compound_chebi .

        ?rhea rdfs:subClassOf                 rh:Reaction     ;
                rh:equation                     ?equation_rhea  ;
                rh:accession                    ?accession_rhea ;
                rh:side/rh:contains/rh:compound ?compound       .
    }
    }
    
    # Somehow, this BIND is necessary for the query to run.
    BIND (?rhea AS ?rhea_id)

    GRAPH <http://sparql.uniprot.org/uniprot> {
        ?uniprot up:reviewed          true     ;
                 up:mnemonic          ?mnemo   ;
                 up:organism          ?taxid   ;
                 up:annotation/
                 up:catalyticActivity/
                 up:catalyzedReaction ?rhea_id .
    }
}
